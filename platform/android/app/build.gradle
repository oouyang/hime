/*
 * HIME Android - App Build Script
 *
 * Copyright (C) 2020 The HIME team, Taiwan
 * License: GNU LGPL v2.1
 */

plugins {
    id 'com.android.application'
}

android {
    namespace 'org.hime.android'
    compileSdk rootProject.ext.compile_sdk

    defaultConfig {
        applicationId "org.hime.android"
        minSdk rootProject.ext.min_sdk
        targetSdk rootProject.ext.target_sdk
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_STL=c++_shared",
                          "-DHIME_ROOT=${rootProject.projectDir}/.."
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    externalNativeBuild {
        cmake {
            path file("src/main/jni/CMakeLists.txt")
            version "3.22.1"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig true
    }

    ndkVersion rootProject.ext.ndk_version

    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets']
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core:1.12.0'

    /* Resolve kotlin-stdlib version conflict */
    constraints {
        implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.22') {
            because 'kotlin-stdlib 1.8+ merges jdk7/jdk8 modules'
        }
        implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22') {
            because 'kotlin-stdlib 1.8+ merges jdk7/jdk8 modules'
        }
    }

    /* Unit testing */
    testImplementation 'junit:junit:4.13.2'

    /* Instrumented testing */
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}
